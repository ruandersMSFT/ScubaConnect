### REQUIRED ###

variable "contact_emails" {
  description = "Emails to notify for alerts and before certificate expiry"
  type        = list(string)
}

variable "resource_group_name" {
  type        = string
  description = "Resource group to create and build resources in"
}

# https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs#environment-1
variable "environment" {
  description = "The environment can be set to 'public' for the global Azure cloud, or 'usgovernment' for the US Government cloud."
  type        = string
  default     = "public"
  validation {
    condition     = contains(["public", "usgovernment"], var.environment)
    error_message = "Environment must be either 'public' or 'usgovernment'."
  }
}


### OPTIONAL ###

variable "location" {
  default     = "East US"
  type        = string
  description = "Azure region to create resources in. Defaults to 'East US'."
}

variable "schedule_interval" {
  default     = "Week"
  type        = string
  description = "The interval to run the scheduled job on."
  validation {
    condition     = contains(["Hour", "Day", "Week", "Month"], var.schedule_interval)
    error_message = "Must be one of 'Hour', 'Day', 'Week', 'Month'"
  }
}

variable "app_name" {
  default     = "ScubaConnect"
  type        = string
  description = "App name. Displayed in Azure console on installed tenants"
}

variable "app_multi_tenant" {
  type        = bool
  default     = false
  description = "If true, the app will be able to be installed in multiple tenants. By default, it is only available in this tenant"
}

variable "vnet" {
  default = null
  type = object({
    address_space          = string
    aci_subnet             = string
    allowed_access_ip_list = list(string)
  })
  description = "Configuration for the vnet, including the address space, ACI subnet, and a list of allowed IP ranges. All strings in CIDR format"
}

variable "firewall" {
  default = null
  type = object({
    resource_group = string
    vnet           = string
    pip            = string
    name           = string
  })
  description = "Configuration for an Azure Firewall; if not null, traffic will be routed through this firewall"
}

variable "serial_number" {
  default     = "01"
  type        = string
  description = "Increment by 1 when re-provisioning with the same resource group name"
}

variable "image_path" {
  default     = "./cisa_logo.png"
  type        = string
  description = "Path to image used for app logo. Displayed in Azure console on installed tenants"
}

### ADVANCED ###

variable "certificate_rotation_period_days" {
  type        = number
  description = "How many days between when the certificate key should be rotated. Note: rotation requires running terraform"
  default     = 30
  validation {
    condition     = var.certificate_rotation_period_days <= 60 && var.certificate_rotation_period_days >= 3
    error_message = "Rotation period must be between 3 and 60 days"
  }
}

variable "create_app" {
  default     = true
  type        = bool
  description = "If true, the app will be created. If false, the app will be imported"
}

variable "prefix_override" {
  default     = null
  type        = string
  description = "Prefix for resource names. If null, one will be generated from app_name"
}

variable "input_storage_container_id" {
  default     = null
  type        = string
  description = "If not null, input container to read configs from (must give permissions to service account). Otherwise by default will create storage container."
}

variable "output_storage_container_id" {
  default     = null
  type        = string
  description = "If not null, output container to put results in (must give permissions to service account). Otherwise by default will create storage container."
}

variable "tenants_dir_path" {
  default     = "./tenants"
  type        = string
  description = "Relative path to directory containing tenant configuration files in yaml"
}

variable "container_registry" {
  type = object({
    server   = string
    username = string
    password = string
  })
  default     = null
  description = "Credentials for logging into registry with container image"
}

variable "container_image" {
  type        = string
  default     = "ghcr.io/cisagov/scubaconnect-m365:latest"
  description = "Docker image to use for running ScubaGear."
}

variable "container_memory_gb" {
  type        = number
  description = "Amount of memory to allocate for ScubaGear container. Due to memory leaks in some dependencies, this may need to be increased if running on many tenants"
  default     = 3
  validation {
    condition     = var.container_memory_gb <= 16 && var.container_memory_gb >= 2
    error_message = "Container memory must be between 2GB and 16GB"
  }
}