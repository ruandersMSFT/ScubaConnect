# Azure Container Instances to run the ScubaGear container
# One group is automatically executed periodically, the other manually
resource "azurerm_container_group" "aci" {
  for_each            = toset(["scheduled", "adhoc"])
  name                = "${var.resource_prefix}-${each.key}-container"
  location            = var.resource_group.location
  resource_group_name = var.resource_group.name
  ip_address_type     = "None"
  subnet_ids          = var.subnet_ids
  os_type             = "Windows"
  restart_policy      = "Never"

  dynamic "image_registry_credential" {
    for_each = var.container_registry == null ? [] : [1]
    content {
      server   = var.container_registry.server
      username = var.container_registry.username
      password = var.container_registry.password
    }
  }

  diagnostics {
    log_analytics {
      workspace_id  = var.log_analytics_workspace.workspace_id
      workspace_key = var.log_analytics_workspace.primary_shared_key
    }
  }

  container {
    name   = "${var.resource_prefix}-container"
    image  = var.container_image
    cpu    = "1"
    memory = var.container_memory_gb
    environment_variables = {
      "RUN_TYPE"                         = each.key
      "TENANT_ID"                        = var.tenant_id
      "APP_ID"                           = var.application_client_id
      "REPORT_OUTPUT"                    = var.output_storage_container_id == null ? azurerm_storage_container.output[0].id : var.output_storage_container_id
      "TENANT_INPUT"                     = var.input_storage_container_id == null ? azurerm_storage_container.input[0].id : var.input_storage_container_id
      "AZCOPY_ACTIVE_DIRECTORY_ENDPOINT" = var.azure_active_directory_endpoint
      "DEBUG_LOG"                        = "false"
    }
    secure_environment_variables = {
      "PFX_B64" = var.application_pfx_b64
    }
  }

  lifecycle {
    ignore_changes = [tags]
  }
}


